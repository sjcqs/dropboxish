apply plugin: 'idea'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        // for gRPC
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.3"
    }
}

subprojects{
    apply plugin: 'java'

    sourceCompatibility = 1.8
    group 'dropboxish'
    version '0.0.1-SNAPSHOT'

    jar {
        manifest {
            attributes 'Main-Class': "com.dropboxish.${project.name}.App"
        }
        doFirst {
            from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    if (it.name == 'pool' || it.name == 'controller'){
        apply plugin: 'com.google.protobuf'

        def grpcVersion = '1.7.0' // CURRENT_GRPC_VERSION
        protobuf {
            protoc {
                artifact = 'com.google.protobuf:protoc:3.5.0'
            }
            plugins {
                grpc {
                    artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
                }
            }
            generateProtoTasks {
                all()*.plugins {
                    grpc {}
                }
            }
        }

        dependencies {
            compile project(':model')
            compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
            compile "io.grpc:grpc-netty:1.7.0"
            compile "io.grpc:grpc-protobuf:1.7.0"
            compile "io.grpc:grpc-stub:1.7.0"
        }

        sourceSets {
            main {
                java {
                    srcDirs 'build/generated/source/proto/main/grpc'
                    srcDirs 'build/generated/source/proto/main/java'
                }
            }
        }
    }
}

